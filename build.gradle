
buildscript {
    ext {
        kotlinVersion = "1.4.21"
        coroutinesVersion = "1.4.2"
        r2dbcVersion = "0.8.3.RELEASE"
        detektVersion = "1.12.0-RC1"
    }
    repositories {
        jcenter()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${detektVersion}"
    }
}

allprojects {
    group = "org.ktorm.r2dbc"
    version = "0.0.1-SNAPSHOT"
}

subprojects { project ->
    apply plugin: "kotlin"
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.bintray"
    apply plugin: "io.gitlab.arturbosch.detekt"
    // apply from: "${project.rootDir}/check-source-header.gradle"

    repositories {
        jcenter()
    }

    dependencies {
        api "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
        api "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
        api "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}"
        api "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:${coroutinesVersion}"
        api "io.r2dbc:r2dbc-spi:${r2dbcVersion}"
        testImplementation "junit:junit:4.12"
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${detektVersion}"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.6"
        kotlinOptions.allWarningsAsErrors = true
        kotlinOptions.freeCompilerArgs = [
            "-Xexplicit-api=strict",
            "-Xopt-in=kotlin.RequiresOptIn"
        ]
    }

    task generateSourcesJar(type: Jar) {
        archiveClassifier = "sources"
        from sourceSets.main.allSource
    }

    task generateJavadoc(type: Jar) {
        archiveClassifier = "javadoc"
    }

    detekt {
        toolVersion = detektVersion
        config = files("${project.rootDir}/detekt.yml")
        reports {
            xml.enabled = false
            html.enabled = false
        }
    }

    publishing {
        publications {
            bintray(MavenPublication) {
                from components.java
                artifact generateSourcesJar
                artifact generateJavadoc

                groupId project.group
                artifactId project.name
                version project.version

                pom {
                    name = project.name
                    description = "A lightweight ORM framework for Kotlin based on R2DBC and Coroutines."
                    url = "https://github.com/kotlin-orm/ktorm-r2dbc"
                    licenses {
                        license {
                            name = "The Apache Software License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "vincentlauvlwj"
                            name = "vince"
                            email = "me@liuwj.me"
                        }
                    }
                    scm {
                        url = "https://github.com/kotlin-orm/ktorm-r2dbc.git"
                    }
                }
            }
        }
    }

    bintray {
        user = System.getenv("BINTRAY_USER")
        key = System.getenv("BINTRAY_KEY")
        publications = ["bintray"]
        publish = true

        pkg {
            repo = "ktorm"
            name = project.name
            licenses = ["Apache-2.0"]
            vcsUrl = "https://github.com/kotlin-orm/ktorm-r2dbc.git"
            labels = ["Kotlin", "ORM", "SQL", "R2DBC", "Coroutines"]

            version {
                name = project.version
                released = new Date()
                vcsTag = project.version

                mavenCentralSync {
                    sync = false
                    user = System.getenv("OSSRH_USER")
                    password = System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }
}

task printClasspath() {
    doLast {
        def jars = subprojects.collect { it.configurations.compileClasspath.getFiles() }.flatten().toSet()
        jars.removeIf { it.name.contains("ktorm-r2dbc") }

        println("Project classpath: ")
        jars.each { println(it.name) }

        def file = file("build/ktorm-r2dbc.classpath")
        file.parentFile.mkdirs()
        file.write(jars.collect { it.absolutePath }.join(File.pathSeparator))
        println("Classpath written to build/ktorm-r2dbc.classpath")
    }
}
